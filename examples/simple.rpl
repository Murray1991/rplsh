####################################################
#################      HELLO!      #################
####################################################

import "business/simple/business.hpp"

# annotate imported variables: "datap true" means that these wrappers
# are suitable for being functional parameters of data parallel skeletons
# like "map" and "reduce"
annotate inc with datap true
annotate square with datap true
annotate redplus with datap true

annotate inc with servicetime 5
annotate square with servicetime 10
annotate redplus with servicetime 5

# inputsize global environment variable affects servicetime for
# datap-annotated wrappers; after inputsize with 100 the servicetime 
# of inc, square and redplus will be multiplied by 100
set inputsize with 100

# dimension variable affects only completion time
set dimension with 50

# resources variable affects optimizations
set resources with 16

# simple parallel expression:
s = pipe(inc, square, redplus)

show s by servicetime, compltime, resources

# alternative datap expressions
d = pipe(map(inc), map(square), reduce(redplus))

# first program: the assignment k = s
#  is necessary since we will rewrite s
k = s
p1 = pipe(vec_source, k, vec_drain)

# rewrite s and d
rewrite s with allrules, allrules
rewrite d with allrules, allrules

# optimize s and d
optimize s with farmopt, pipeopt, maxresources
optimize d with mapopt, farmopt, pipeopt, maxresources

# show the top-5 results in servicetime for s and d
show s by servicetime, compltime, resources +5
show d by servicetime, compltime, resources +5

####################################################
####### Pick the best and generate code! ###########
####################################################
